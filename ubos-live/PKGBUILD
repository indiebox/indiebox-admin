developer=http://indiecomputing.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.31
pkgrel=1
pkgdesc="Device-side functionality of UBOS Live"
arch=('any')
license=("PPL3")
options=('!strip')
depends=(
    'coreutils'
    'openvpn'
    'perl-www-curl'
    'ubos-admin'
    'wireguard-tools'
)
_vendor_perl=/usr/share/perl5/vendor_perl
install=install

package() {
# Config
    mkdir -p -m755 ${pkgdir}/etc/ubos-live

# ubos-live user
    install -D -m644 ${startdir}/sysusers.d/ubos-live.conf -t ${pkgdir}/usr/lib/sysusers.d/
    mkdir -m750 ${pkgdir}/etc/sudoers.d
    install -D -m600 ${startdir}/sudoers.d/ubos-live       -t ${pkgdir}/etc/sudoers.d/

# ssh keys
    install -D -m644 ${startdir}/ssh/authorized_keys -t ${pkgdir}/usr/share/ubos-live

# Systemd
    install -D -m644 ${startdir}/systemd/* -t ${pkgdir}/usr/lib/systemd/system/

# Initialize on boot with staff
    install -D -m644 ${startdir}/etc/ubos/staff-boot-callbacks/* -t ${pkgdir}/etc/ubos/staff-boot-callbacks/

# Templates
    install -D -m644 ${startdir}/tmpl/* -t ${pkgdir}/usr/share/${pkgname}/tmpl/

# Code
    install -D -m755 ${startdir}/vendor_perl/UBOS/Commands/*.pm            -t ${pkgdir}${_vendor_perl}/UBOS/Commands/
    install -D -m755 ${startdir}/vendor_perl/UBOS/Live/*.pm                -t ${pkgdir}${_vendor_perl}/UBOS/Live/
    install -D -m755 ${startdir}/vendor_perl/UBOS/Live/StaffCallbacks/*.pm -t ${pkgdir}${_vendor_perl}/UBOS/Live/StaffCallbacks/
    install -D -m755 ${startdir}/bin/*                                     -t ${pkgdir}/usr/share/${pkgname}/bin/

# Insert inlined images
    perl -p -i \
        -e "s!UBOS_STAFF_IMAGE_BASE64!$(base64 ${startdir}/images/ubos-staff.png | tr -d '\n')!" \
        ${pkgdir}${_vendor_perl}/UBOS/Live/UbosLiveHtmlConstants.pm
    perl -p -i \
        -e "s!HELP_IMAGE_BASE64!$(base64 ${startdir}/images/help.png | tr -d '\n')!" \
        ${pkgdir}${_vendor_perl}/UBOS/Live/UbosLiveHtmlConstants.pm
}
